plugins {
    id 'java-library'
    id 'com.google.protobuf' version '0.9.4'
    id 'maven-publish'
}

description = 'LOTECS Auth gRPC API (Protocol Buffers)'

group = libraryGroup
version = '1.0.0-SNAPSHOT'

dependencies {
    // Protocol Buffers
    api 'com.google.protobuf:protobuf-java'
    api 'io.grpc:grpc-protobuf'
    api 'io.grpc:grpc-stub'
    api 'javax.annotation:javax.annotation-api:1.3.2'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.25.1'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.63.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

jar {
    enabled = true
}

tasks.named('sourcesJar') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(Javadoc).configureEach {
    options {
        encoding = 'UTF-8'
        charSet = 'UTF-8'
        addBooleanOption('Xdoclint:none', true)
    }
    failOnError = false
}

